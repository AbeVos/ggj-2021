title: introduction
tags:
---
<<set $emotion to "neutral">>
X: ...?
<<change_mood Who 1>>
<<set $emotion to "happy">>
X: Look who it is!
Internal: Wait! Do I know them?
X: {$player_name}!
X: I haven't seen you in ages!
Internal: Oh no! I forgot their name!

-> Hi... you!
	X: It's been too long!
-> Who are you again?
	X: You always where a joker, {$player_name}!
	X: Surely, you remember me!
-> Hey... old friend!
	X: It's so good to see you after all this time!
-> *Just smile and wave*
	X: What are the odds of running into you here?
[[wedding]]
===

title: wedding
tags:
---
<<set $emotion to "neutral">>
X: When was the last time we spoke?
Internal: I need to figure out their name.
Internal: Or else this will become really awkward.
X: It must have been at that {$tag_wedding} some two years ago.
X: You remember that, don't you?
X: That was some idea. Planning an outdoor {$tag_wedding} in the dead of winter.
X: At least the reception was nice.
X: Even despite the bride's best friend performing with their GENRE band.
X: Wasn't a great fan of that.
X: Also, I was not sold on the {$tag_cake}.
X: It had a very strange taste to it.
<<set $emotion to "sad">>
X: Like it had been drenched into something.
X: Very strange..
<<set $emotion to "neutral">>
X: Anyway, surely you remember that day?

-> Sure I do, Adela and Billie looked so happy that night!
	<<change_mood Who 1>>
	<<set $emotion to "happy">>
	X: So you DO remember!
-> Sure I do, Davy and Elliott looked so happy together!
	<<change_mood Who -1>>
	<<set $emotion to "sad">>
	X: ...
	X: I'm not quite sure we are talking about the same wedding.
	X: It was Adela and Billie's wedding!
	Internal: Of course...
	X: Don't worry, it happens to the best of us.
-> Of course I do! That was quite a... wedding wasn't it?
	<<change_mood Who 0>>
	<<set $emotion to "neutral">>
	X: Yeah...
	X: I guess it was.

Internal: That was a close one.
Internal: Now, what was their name again?
[[concert]]
===

title: concert
tags:
---
<<set $emotion to "neutral">>
X: Anyway..
X: Didn't that happen in the same year as my first {$tag_concert}?
X: Well, not really my first of course.
<<set $emotion to "sad">>
X: But it was the first since after the {$tag_accident}.
<<set $emotion to "neutral">>
X: I was pretty anxious about it.
X: It wasn't easy, given the situation with my arm.
<<set $emotion to "sad">>
X: Of course, it didn't help my self-confidence that no one else showed up.
<<set $emotion to "neutral">>
X: It happens, I suppose.
X: What can you do, right?
<<set $emotion to "happy">>
X: It didn't really matter anyway, since you where there, weren't you?

-> Absolutely! You where... Making music... And it was great!
	<<change_mood Who -1>>
	<<set $emotion to "sad">>
	X: Yeah...
	<<set $emotion to "neutral">>
	X: It was great.
-> Yes, you where great playing your trombone. A beautiful rendition of Beethoven.
	<<change_mood Who +2>>
	<<set $emotion to "happy">>
	X: Yes!
	X: That was a great time.
	X: It really meant a lot to me for you to be there.
	X: After everything that happened between us.
-> Playing GENRE on your trombone was a masterful stroke!
	<<change_mood Who -1>>
	<<set $emotion to "neutral">>
	X: What?
	X: I don't even like GENRE!
	<<set $emotion to "sad">>
	X: I guess you weren't there after all...
	X: That's fine.
-> Very clever how you used your INSTRUMENT to play GENRE.
	<<change_mood Who -2>>
	X: That's not right at all!
	<<set $emotion to "sad">>
	X: I guess you weren't there after all...
	X: That's okay.

[[accident]]
===

title: accident
tags:
---
X: I guess we haven't been seeing much of each other after that.
X: Of course, it has been a difficult time for all of us after the {$tag_accident}.
X: Can you believe it has been three years already?
<<set $emotion to "sad">>
X: It came so unexpectedly.
X: One moment you're going about your day, next you wake up in a hospital.
X: It sure wasn't great walking around with that {$tag_cast}.
<<set $emotion to "happy">>
X: But everyone wanted to sign it, so at least that was fun.
<<set $emotion to "neutral">>

<<if $mood >= 0>>
	<<set $emotion to "neutral">>
	X: You remember that day...
	X: Right?
<<endif>>
<<if $mood < 0>>
	<<set $emotion to "sad">>
	X: But I don't suppose you remember that either, do you?
<<endif>>

-> I remember, you broke your arm on that faulty escalator.
	<<change_mood Who -3>>
	<<set $emotion to "sad">>
	X: What?
	X: It wasn't an escalator.
	X: I broke my arm after you hit me with your car!
-> If I recall correctly, you broke your leg on that broken escalator.
	<<change_mood Who -3>>
	<<set $emotion to "sad">>
	X: What are you talking about?
	X: You hit me with your car and broke my arm!
	X: You seriously do not remember?!
-> I remember. The car crash, when you broke your arm.
	<<change_mood Who 3>>
	<<set $emotion to "neutral">>
	X: That's the one.
	X: Of course, you could hardly forget.
	<<set $emotion to "sad">>
	X: After all, you where the one driving.
-> I recall! You crashed your car and broke your arm.
	<<change_mood Who -2>>
	<<set $emotion to "sad">>
	X: What?
	X: No!
	X: You hit me with your car!

<<if $mood > 2>>
	[[happy_ending]]
<<endif>>
<<if $mood >= 1 and $mood <= 2>>
	[[I'm really sorry about that, Kim.|wrong_name]]
	[[I'm really sorry about that, Lindsay.|wrong_name]]
	[[I'm really sorry about that, Max.|correct_name]]
	[[I'm really sorry about that, Nikita.|wrong_name]]
<<endif>>
<<if $mood < 0>>
	[[sad_ending]]
<<endif>>
===

title: wrong_name
tags:
---
<<set $emotion to "neutral">>
X: Excuse me?
<<set $emotion to "sad">>
X: You don't remember my name?
X: Well, this is awkward..
[[sad_ending]]
===

title: correct_name
tags:
---
<<set $emotion to "happy">>
X: For a moment I thought that you had forgotten my name.
X: But you would never do something like that!
[[happy_ending]]
===

title: happy_ending
tags:
---
<<set $emotion to "happy">>
X: Anyway, this is my floor.
X: It was great seeing you again!
X: I'll see you around, {$player_name}!
===

title: sad_ending
tags:
---
<<set $emotion to "sad">>
X: Well I guess we weren't such good friends after all...
X: I have to go now.
===
