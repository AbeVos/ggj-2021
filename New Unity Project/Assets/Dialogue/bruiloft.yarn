title: introduction
tags:
---
<<set $emotion to "neutral">>
<<change_mood Who 1>>
<<set $emotion to "happy">>
...: Look who it is!
Internal: Wait! Do I know them?
...: {$player_name}!
...: I haven't seen you in ages!
Internal: Oh no! I forgot their name!

-> Hi... you!
	...: It's been too long!
-> Who are you again?
	...: You always where a joker, {$player_name}!
	...: Surely, you remember me!
-> Hey... old friend!
	...: It's so good to see you after all this time!
-> *Just smile and wave*
	...: What are the odds of running into you here?
[[wedding]]
===

title: wedding
tags:
---
<<set $emotion to "neutral">>
...: When was the last time we spoke?
Internal: I need to figure out their name.
Internal: Or else this will become really awkward.
...: It must have been at that {$tag_wedding} some two years ago. You remember that, don't you?
...: That was some idea. Planning an outdoor {$tag_wedding} in the dead of winter. At least the reception was nice.
...: Even despite the bride's best friend performing with their Ska band. Wasn't a great fan of that.
...: Also, I was not sold on the {$tag_cake}. It had a very strange taste to it.
<<set $emotion to "sad">>
...: Like it had been drenched into something. Very strange...
<<set $emotion to "neutral">>
...: Anyway, surely you remember that day?

-> Sure I do, Adela and Billie looked so happy that night!
	<<change_mood Who 1>>
	<<set $emotion to "happy">>
	...: So you DO remember!
-> Sure I do, Davy and Elliott looked so happy together!
	<<change_mood Who -1>>
	<<set $emotion to "sad">>
	...: ...
	...: I'm not quite sure we are talking about the same wedding.
	...: It was Adela and Billie's wedding!
	Internal: Of course...
	...: Don't worry, it happens to the best of us.
-> Of course I do! That was quite a... wedding wasn't it?
	<<change_mood Who 0>>
	<<set $emotion to "neutral">>
	...: Yeah...
	...: I guess it was.

Internal: That was a close one.
Internal: Now, what was their name again?
[[concert]]
===

title: concert
tags:
---
<<set $emotion to "neutral">>
...: Anyway...
...: Didn't that happen in the same year as my first {$tag_concert}?
...: Well, not really my first of course.
<<set $emotion to "sad">>
...: But it was the first since after the {$tag_accident}.
<<set $emotion to "neutral">>
...: I was pretty anxious about it. It wasn't easy, given the situation with my arm.
<<set $emotion to "sad">>
...: Of course, it didn't help my self-confidence that no one else showed up.
<<set $emotion to "neutral">>
...: It happens, I suppose. What can you do, right?
<<set $emotion to "happy">>
...: It didn't really matter anyway. After all you where there, weren't you?

-> Absolutely! You where... Making music... And it was great!
	<<change_mood Who -1>>
	<<set $emotion to "sad">>
	...: Yeah...
	<<set $emotion to "neutral">>
	...: It was great.
-> Yes, you where great playing your trombone. A beautiful rendition of Beethoven.
	<<change_mood Who +2>>
	<<set $emotion to "happy">>
	...: Yes!
	...: That was a great time. It really meant a lot to me for you to be there.
	...: After everything that happened between us.
-> Playing Ska on your trombone was a masterful stroke!
	<<change_mood Who -1>>
	<<set $emotion to "neutral">>
	...: What?
	...: I don't even like Ska!
	<<set $emotion to "sad">>
	...: I guess you weren't there after all...
	...: That's fine.
-> Very clever how you used your INSTRUMENT to play Ska.
	<<change_mood Who -2>>
	...: That's not right at all!
	<<set $emotion to "sad">>
	...: I guess you weren't there after all...
	...: That's okay.

[[accident]]
===

title: accident
tags:
---
...: I guess we haven't been seeing much of each other after that.
...: Of course, it has been a difficult time for all of us after the {$tag_accident}.
...: Can you believe it has been three years already?
<<set $emotion to "sad">>
...: It came so unexpectedly.
...: One moment you're going about your day, next you wake up in a hospital.
...: It sure wasn't great walking around with that {$tag_cast}.
<<set $emotion to "happy">>
...: But everyone wanted to sign it, so at least that was fun.
<<set $emotion to "neutral">>

<<if $mood >= 0>>
	<<set $emotion to "neutral">>
	...: You remember that day...
	...: Right?
<<endif>>
<<if $mood < 0>>
	<<set $emotion to "sad">>
	...: But I don't suppose you remember that either, do you?
<<endif>>

-> I remember, you broke your arm on that faulty escalator.
	<<change_mood Who -3>>
	<<set $emotion to "sad">>
	...: What?
	...: It wasn't an escalator.
	...: I broke my arm after you hit me with your car!
-> If I recall correctly, you broke your leg on that broken escalator.
	<<change_mood Who -3>>
	<<set $emotion to "sad">>
	...: What are you talking about?
	...: You hit me with your car and broke my arm!
	...: You seriously do not remember?!
-> I remember. The car crash, when you broke your arm.
	<<change_mood Who 3>>
	<<set $emotion to "neutral">>
	...: That's the one.
	...: Of course, you could hardly forget.
	<<set $emotion to "sad">>
	...: After all, you where the one driving.
-> I recall! You crashed your car and broke your arm.
	<<change_mood Who -2>>
	<<set $emotion to "sad">>
	...: What?
	...: No!
	...: You hit me with your car!

<<if $mood >= 5>>
	[[happy_ending]]
<<endif>>
<<if $mood >= 1 and $mood < 5>>
	[[I'm really sorry about that, Kim.|wrong_name]]
	[[I'm really sorry about that, Lindsay.|wrong_name]]
	[[I'm really sorry about that, Max.|correct_name]]
	[[I'm really sorry about that, Nikita.|wrong_name]]
<<endif>>
<<if $mood < 1>>
	[[sad_ending]]
<<endif>>
===

title: wrong_name
tags:
---
<<set $emotion to "neutral">>
...: Excuse me?
<<set $emotion to "sad">>
...: You don't remember my name?
...: Well, this is awkward..
[[sad_ending]]
===

title: correct_name
tags:
---
<<set $emotion to "happy">>
Max: For a moment I thought that you had forgotten my name.
Max: But you would never do something like that!
[[happy_ending]]
===

title: happy_ending
tags:
---
<<set $emotion to "happy">>
Max: Anyway, this is my floor.
Max: It was great seeing you again!
Max: I'll see you around, {$player_name}!
===

title: sad_ending
tags:
---
<<set $emotion to "sad">>
...: Well I guess we weren't such good friends after all...
...: I have to go now.
===
